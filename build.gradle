plugins {
   id 'us.ihmc.gradle.ihmc-build' version '0.0.13'
}


apply plugin: 'maven-publish'

sourceCompatibility = 1.7
targetCompatibility = 1.7
version = '0.2.1'

sourceSets {
	main {
		java {
			srcDirs = ['src']
		}
	}
}

task sourceJar(type: Jar) {
	from sourceSets.main.allJava
}

jar {
   manifest {
      attributes(
      "Created-By": "IHMC Gradle Build Script",
      "Implementation-Title": project.name,
      "Implementation-Version": project.version,
      "Implementation-Vendor": "IHMC",
      
      "Bundle-Name": project.name,
      "Bundle-Version": project.version,
      "Bundle-License": "http://www.apache.org/licenses/LICENSE-2.0.txt",
      "Bundle-Vendor": "IHMC")
   }
}

publishing {
	publications {
		mavenJava(MavenPublication) {

			groupId 'us.ihmc'
			artifactId project.name
			version "$version"
			from components.java
			
			pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name project.name
                    url 'https://github.com/ihmcrobotics/ihmc-ros-control'
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'jsmith'
                            name 'Jesper Smith'
                            email 'jsmith@ihmc.us'
                        }
                        developer {
                        	id 'dstephen'
                        	name 'Douglas Stephen'
                        	email 'dstephen@ihmc.us'
                        }
                    }
                }

        	}
			
			artifact sourceJar {
				classifier "sources"
			}
		}

	}
}
